plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version "${javafxpluginVersion}"
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

javafx {
    version = javafxVersion
    modules = ['javafx.base']
}

// Expose test code/resources in commons as another jar, so that it can be re-used in other libraries for testing.
configurations {
    testClasses {
        extendsFrom(testRuntime)
    }
}
task testJar(type: Jar) {
    archiveClassifier.set('test')
    from sourceSets.test.output
}
artifacts {
    testClasses testJar // Add the jar generated by the testJar task to the testClasses configuration
}

dependencies {
    api "com.google.code.findbugs:jsr305:${findbugsVersion}"
    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"
    implementation "org.fxmisc.easybind:easybind:${easybindVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.hamcrest:hamcrest:${hamcrestVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
}

test {
    useJUnitPlatform()
}
